apply plugin: 'java'
group 'io.cdep'


repositories {
    jcenter()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.vrallev.gradle:jarjar-gradle:1.1.0'
    }
}

apply plugin: 'net.vrallev.jarjar'


jarjar {
	// required, path to the executable .jar file
    jarJarFile '../tools/jarjar-1.3.jar'

    // optional, the rules for the jarjar tool
    rules = [
            'rule org.yaml.** ext.org.yaml.@1'
    ]

    // optional, exclude files from the dependency .jar files
    srcExcludes = ['META-INF/**']

    // optional, default is build_repackaged.jar, the result .jar file name
    //outputName 'build_repackaged.jar'

    // optional, default is libs, the directory of the result .jar
    //outputDir 'libs'

    // optional, default is false
    ignoreJarJarResult false
}


if (System.getenv()["TRAVIS_TAG"] != null && System.getenv()["TRAVIS_TAG"].length() > 0) {
    version System.getenv()["TRAVIS_TAG"]
} else {
    version 'dev'
}


class GenerateBuildInfo extends DefaultTask {
    @OutputFile
    public File buildInfoFile = new File("cdep/src/main/java/io/cdep/BuildInfo.java")

    @TaskAction
    generate() {
        buildInfoFile.parentFile.mkdirs()
        buildInfoFile.write(
                """package io.cdep;
class BuildInfo {
  static final String PROJECT_VERSION = "$project.version";
}""")
    }
}

task generateBuildInfo(type: GenerateBuildInfo)
//noinspection GroovyAssignabilityCheck
compileJava.dependsOn generateBuildInfo
//compileJava.source generateBuildInfo.buildInfoFile


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    //noinspection GroovyAssignabilityCheck
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    jarjar 'org.yaml:snakeyaml:1.17'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.google.guava', name: 'guava', version: '20.0'
    testCompile group: 'com.google.truth', name: 'truth', version: '0.31'
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "org.mockito:mockito-core:2.+"
}
